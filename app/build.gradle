plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

ext {
    versions = [
        compileSdk: 33,
        minSdk: 21,
        targetSdk: 33,
        okhttp: '4.9.0',
        coreKtx: '1.8.0',
        constraintLayout: '2.2.0-alpha07',
        kotlinReflect: '1.6.21',
        kotlinCoRoutines: '1.6.4',
        gson: '2.10.1',
        appcompat: '1.6.1'
    ]
    versionMajor = 3
    versionMinor = 1
    versionPatch = 0
    grindrVersion = '24.4.0'
}

android {
    namespace 'com.grindrplus'
    compileSdk 34

    defaultConfig {
        applicationId 'com.grindrplus'
        minSdk versions.minSdk
        targetSdk versions.targetSdk
        versionCode 14
        versionName "${versionMajor}.${versionMinor}.${versionPatch}-${grindrVersion}"
        buildConfigField 'String', 'TARGET_GRINDR_VERSION', "\"${grindrVersion}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    android.applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            outputFileName = "GPlus_v${variant.versionName}.apk"
        }
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.2.0'
    implementation 'com.google.android.material:material:1.6.1'
    compileOnly fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.core:core-ktx:${versions.coreKtx}"
    implementation "androidx.appcompat:appcompat:${versions.appcompat}"
    implementation "com.squareup.okhttp3:okhttp:${versions.okhttp}"
    implementation "androidx.constraintlayout:constraintlayout:${versions.constraintLayout}"
    implementation "org.jetbrains.kotlin:kotlin-reflect:${versions.kotlinReflect}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.kotlinCoRoutines}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.kotlinCoRoutines}"
    implementation "com.google.code.gson:gson:${versions.gson}"
}
